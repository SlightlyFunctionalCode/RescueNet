plugins {
    id 'org.springframework.boot' version '3.3.5'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'java'
    id 'application'
}

group = 'org.estg.ipp.pt'
version = '1.0-SNAPSHOT'

application {
    // Define uma classe principal padrão (opcional, se precisar de uma execução direta)
    mainClass = 'org.estg.ipp.pt.Server'
}

bootJar {
    archiveBaseName.set('server')  // Nome base para o arquivo JAR
    version = '1.0-SNAPSHOT'  // Versão do JAR
    mainClass.set('org.estg.ipp.pt.Server')  // Classe principal do Spring Boot para o Server
}

jar {
    enabled = false  // Desabilitar a criação de JAR "normal" (não com Spring Boot)
}

repositories {
    mavenCentral()
}

dependencies {
    // PDF Generator
    implementation 'com.itextpdf:itextpdf:5.5.13.4'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    // Spring Boot Starter Data JPA
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    // Postgresql Connector
    implementation 'org.postgresql:postgresql:42.7.3' // Use the latest stable version
    // Spring Boot Starter Security
    implementation 'org.springframework.boot:spring-boot-starter-security'
    // JWT
    implementation 'io.jsonwebtoken:jjwt:0.9.1'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.register('runClient', JavaExec) {
    group = 'application'
    description = 'Run the Client main class'
    mainClass.set('org.estg.ipp.pt.Client')
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
}

tasks.register('runServer', JavaExec) {
    group = 'application'
    description = 'Run the Server main class'
    mainClass.set('org.estg.ipp.pt.Server')
    classpath = sourceSets.main.runtimeClasspath
}


// Configuração do JAR para o cliente
task clientJar(type: Jar) {
    archiveBaseName = 'client'
    from sourceSets.main.output
    manifest {
        attributes 'Main-Class': 'org.estg.ipp.pt.Client'
    }
}


build.dependsOn clientJar

test {
    useJUnitPlatform()
}